plugins {
    id 'java'
    id 'com.github.spotbugs' version '5.0.13'
    id 'checkstyle'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.9.0'
}

version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    // https://mvnrepository.com/artifact/org.easymock/easymock
    testImplementation group: 'org.easymock', name: 'easymock', version: '5.1.0'
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

test {
    useJUnitPlatform()
}

build.dependsOn 'pitest'

pitest {
    targetClasses = ['controller.*', 'model.*']
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    junit5PluginVersion = '1.0.0'
}
